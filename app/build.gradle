
plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'com.google.dagger.hilt.android'
    id 'com.google.gms.google-services'
}

android {
    compileSdk 34

    def _patch
    def versionPropsFile = file("version.properties")

    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()
        versionProps.load(new FileInputStream(versionPropsFile))
        _patch = versionProps['PATCH'].toInteger() + 1
        versionProps['PATCH'] = _patch.toString()
        versionProps.store(versionPropsFile.newWriter(), null)
    } else {
        throw new GradleException("Could not read version.properties!")
        //_patch = "E"
    }
    def _versionName = "3." + _patch

    defaultConfig {
        applicationId "ua.com.programmer.qrscanner"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode _patch
        versionName _versionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    dataBinding {
        enabled true
    }
    namespace 'ua.com.programmer.barcodetest'
}

dependencies {
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    //implementation fileTree(include: ['*.jar'], dir: 'libs')
//    androidTestImplementation('androidx.test.espresso:espresso-core:3.2.0', {
//        exclude group: 'com.android.support', module: 'support-annotations'
//    })

    // CameraX dependencies
    def camera_version = "1.2.3"
    implementation "androidx.camera:camera-camera2:$camera_version"
    implementation "androidx.camera:camera-lifecycle:$camera_version"
    implementation "androidx.camera:camera-view:$camera_version"

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    //implementation 'com.google.android.gms:play-services-vision:20.1.3'
    implementation 'com.google.mlkit:barcode-scanning:17.2.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:31.2.2')
    //implementation 'com.google.firebase:firebase-core'
    implementation 'com.google.firebase:firebase-firestore'
    implementation 'com.google.firebase:firebase-auth'

    // Hilt
    implementation "com.google.dagger:hilt-android:2.47"
    kapt "com.google.dagger:hilt-compiler:2.47"

    testImplementation 'junit:junit:4.13.2'
}

// Hilt
// Allow references to generated code
kapt {
    correctErrorTypes true
}